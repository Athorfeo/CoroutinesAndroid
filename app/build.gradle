apply plugin: 'com.android.application'
apply plugin: 'io.fabric'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: 'kotlin-allopen'
apply plugin: 'androidx.navigation.safeargs'

allOpen {
    // allows mocking for classes w/o directly opening them for release builds
    annotation 'com.athorfeo.source.testing.OpenClass'
}

android {
    compileSdkVersion 29
    buildToolsVersion "29.0.2"
    defaultConfig {
        applicationId "com.athorfeo.coroutines"
        minSdkVersion 21
        targetSdkVersion 29
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "com.athorfeo.source.util.AppTestRunner"

        buildConfigField 'String', 'apyKey', apiKey
    }
    buildTypes {
        debug{
            applicationIdSuffix ".debug"
            debuggable true
            buildConfigField 'String', 'urlBase', urlDevBase
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            buildConfigField 'String', 'urlBase', urlProBase
        }
    }
    defaultConfig{
        vectorDrawables.useSupportLibrary = true
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    testOptions {
        unitTests.returnDefaultValues = true
    }
    kotlinOptions {
        jvmTarget = "1.8"
    }
    lintOptions {
        checkAllWarnings true
        warningsAsErrors true
        abortOnError false
    }
    packagingOptions {
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/license.txt'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/notice.txt'
        exclude 'META-INF/ASL2.0'
        exclude 'META-INF/INDEX.LIST'
    }
    dataBinding {
        enabled = true
    }
    sourceSets {
        androidTest.java.srcDirs += "src/test-common/java"
        test.java.srcDirs += "src/test-common/java"
    }
    kapt {
        generateStubs = true
    }

    /* Flavors */
    flavorDimensions "version"
    productFlavors {}
}

dependencies {
    def app_compat_version = "1.1.0-rc01"
    def core_version = "1.2.0-alpha04"
    def recyclerview_version = "1.1.0-beta04"
    def lifecycle_version = "2.2.0-alpha04"
    def fragment_ktx_version = "1.2.0-alpha03"
    def core_testing_version = "2.0.1"
    def constraintlayout_version = "1.1.3"
    def cardview_version = "1.0.0"
    def material_version = "1.0.0"
    def navigation_version = "1.0.0"
    def retrofit_version = "2.6.0"
    def logging_interceptor_version = "3.6.0"
    def glide_version = "4.9.0"
    def room_version = "2.1.0"
    def dagger_version = "2.16"
    def leakcanary_version = "2.0-beta-2"
    def preference_version = "1.0.0"
    def timber_version = "4.7.1"
    def espresso_core_version = "3.2.0"
    def mockito_version = "3.1.0"
    def mockito_android_version = "2.25.0"
    def coroutines_version = "1.3.1"
    def testing_version = "1.2.0"

    implementation fileTree(dir: 'libs', include: ['*.jar'])

    /* Kotlin */
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"

    /* Default */
    implementation "androidx.core:core-ktx:$core_version"
    implementation "androidx.appcompat:appcompat:$app_compat_version"
    implementation "androidx.fragment:fragment-ktx:$fragment_ktx_version"
    implementation "androidx.preference:preference:$preference_version"

    /* Coroutines */
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$coroutines_version"

    /* view */
    implementation "androidx.constraintlayout:constraintlayout:$constraintlayout_version"
    implementation "androidx.recyclerview:recyclerview:$recyclerview_version"
    implementation "androidx.cardview:cardview:$cardview_version"

    /* Material */
    implementation "com.google.android.material:material:$material_version"

    /* Navigation */
    implementation "android.arch.navigation:navigation-fragment-ktx:$navigation_version"
    implementation "android.arch.navigation:navigation-ui-ktx:$navigation_version"

    /* Lifecycle */
    implementation "androidx.lifecycle:lifecycle-extensions:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-runtime-ktx:$lifecycle_version"
    //kapt "androidx.lifecycle:lifecycle-compiler:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-common-java8:$lifecycle_version"

    /* Retrofit */
    implementation "com.squareup.retrofit2:retrofit:$retrofit_version"
    implementation "com.squareup.retrofit2:converter-gson:$retrofit_version"
    implementation "com.squareup.okhttp3:logging-interceptor:$logging_interceptor_version"

    /* Glide */
    implementation "com.github.bumptech.glide:glide:$glide_version"
    kapt "com.github.bumptech.glide:compiler:$glide_version"

    /* Room */
    implementation "androidx.room:room-runtime:$room_version"
    implementation "androidx.room:room-ktx:$room_version"
    kapt "androidx.room:room-compiler:$room_version"

    /* Dagger 2 */
    implementation "com.google.dagger:dagger:$dagger_version"
    implementation "com.google.dagger:dagger-android:$dagger_version"
    implementation "com.google.dagger:dagger-android-support:$dagger_version"
    kapt "com.google.dagger:dagger-compiler:$dagger_version"
    kapt "com.google.dagger:dagger-android-processor:$dagger_version"

    /* LeakCanary */
    debugImplementation "com.squareup.leakcanary:leakcanary-android:$leakcanary_version"

    /* Timber */
    implementation "com.jakewharton.timber:timber:$timber_version"

    /* Firebase */
    implementation 'com.google.firebase:firebase-analytics:17.2.1'
    implementation 'com.crashlytics.sdk.android:crashlytics:2.10.1'

    /* Library */
    implementation project(":utilities")

    /* Test Unit */
    testImplementation 'junit:junit:4.12'
    androidTestImplementation "androidx.test:core:$testing_version"
    androidTestImplementation "androidx.test:runner:$testing_version"
    androidTestImplementation "androidx.test:rules:$testing_version"
    androidTestImplementation "androidx.test.ext:truth:$testing_version"
    androidTestImplementation "androidx.test.ext:junit:1.1.1"
    androidTestImplementation "com.google.truth:truth:0.42"

    testImplementation "androidx.arch.core:core-testing:$core_testing_version"
    testImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-test:$coroutines_version"

    /* Espresso */
    androidTestImplementation "androidx.test.espresso:espresso-core:$espresso_core_version"
    androidTestImplementation "androidx.test.espresso:espresso-contrib:$espresso_core_version"
    androidTestImplementation "androidx.test.espresso:espresso-intents:$espresso_core_version"
    androidTestImplementation "androidx.test.espresso:espresso-accessibility:$espresso_core_version"
    androidTestImplementation "androidx.test.espresso:espresso-web:$espresso_core_version"
    androidTestImplementation "androidx.test.espresso.idling:idling-concurrent:$espresso_core_version"
    androidTestImplementation "androidx.test.espresso:espresso-idling-resource:$espresso_core_version"

    /* Room */
    androidTestImplementation "androidx.room:room-testing:$room_version"

    /* Mockito */
    testImplementation "org.mockito:mockito-core:$mockito_version"
    testImplementation "org.mockito:mockito-inline:$mockito_version"
    androidTestImplementation "org.mockito:mockito-core:$mockito_version"
    androidTestImplementation "org.mockito:mockito-android:$mockito_android_version"
}

apply plugin: 'com.google.gms.google-services'